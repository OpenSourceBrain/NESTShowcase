"""
Name: izh2...

"""
neuron izh2:

  initial_values:
    V_m mV = -65 mV # Membrane potential
    U_m real = 0   # Membrane potential recovery variable
  end

  equations:
    V_m' = ( 0.04 * V_m * V_m / mV + 5.0 * V_m + ( 140 - U_m ) * mV + ( (I_e + I_stim) * GOhm ) ) / ms
    U_m' = a*(b*V_m-U_m * mV) / (mV*ms)
  end

  parameters:
    a real = 0.02   # describes time scale of recovery variable
    b real = 0.2    # sensitivity of recovery variable
    c mV = -65 mV     # after-spike reset value of V_m
    d real = 8.0    # after-spike reset value of U_m
    V_min mV = -inf * mV # Absolute lower value for the membrane potential.

    # constant external input current
    I_e pA = 0 pA
  end

  input:
    spikes mV <- spike
    I_stim pA <- current
  end

  output: spike

  update:
    integrate_odes()
    # Add synaptic current
    V_m += spikes

    # lower bound of membrane potential
    V_m = (V_m < V_min)? V_min : V_m

    # threshold crossing
    if V_m >= 30 mV:
      V_m = c
      U_m += d
      emit_spike()
    end

  end

end
